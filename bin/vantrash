#!/usr/bin/perl
use strict;
use warnings;
use LWP::UserAgent;
use Getopt::Long;
use JSON::XS 'encode_json';

my $zone;
my $id;
my $name;
my $password;
my $email;
my $offset;
my $target;
my $delete;
my $confirm;
my $server_base = 'http://vantrash.ca';
my $payment_period;
GetOptions(
    'z|zone=s'     => \$zone,
    'i|id=s'       => \$id,
    'n|name=s'     => \$name,
    'p|password=s' => \$password,
    'e|email=s'    => \$email,
    'o|offset=s'   => \$offset,
    't|target=s'   => \$target,
    'period'       => \$payment_period,
    'delete'       => \$delete,
    'confirm'      => \$confirm,
    'server=s'     => \$server_base,
) or usage();

if ($delete) {
    usage() unless $zone and $id;
    delete_reminder();
}
elsif ($confirm) {
    usage() unless $zone and $id;
    confirm_reminder();
}
else {
    usage() unless $zone and $name and $email;
    add_reminder();
}
exit;

sub delete_reminder {
    my $uri = "$server_base/zones/$zone/reminders/$id";
    my $ua = LWP::UserAgent->new;
    my $req = HTTP::Request->new('DELETE', $uri);
    my $resp = $ua->request($req);
    print $resp->status_line, "\n";
}

sub confirm_reminder {
    my $uri = "$server_base/zones/$zone/reminders/$id/confirm";
    my $ua = LWP::UserAgent->new;
    my $req = HTTP::Request->new('GET', $uri);
    my $resp = $ua->request($req);
    print $resp->status_line, "\n";
}

sub add_reminder {
    my $uri = "$server_base/zones/$zone/reminders";
    my $ua = LWP::UserAgent->new;
    my $req = HTTP::Request->new('POST', $uri);
    $req->content( encode_json( {
                name => $name,
                email => $email,
                ($password ? (password => $password) : ()),
                ($offset   ? (offset   => $offset  ) : ()),
                ($target   ? (target   => $target  ) : ()),
                ($payment_period   ? (payment_period => $payment_period ) : ()),
            }
        )
    );

    my $resp = $ua->request($req);
    print $resp->status_line, "\n";
    print $resp->headers->header('Location'), "\n";
    print $server_base, $resp->content, "\n";
}


sub usage {
    die <<EOT
USAGE: $0 <options>

Creating a reminder:
   $0 --name foo --email your\@email --zone your-zone 
   Optional: [--offset 'offset'] [--target 'twitter:user']

Confirming a reminder:
   $0 --confirm --id confirm_hash --zone zone

Deleting a reminder:
   $0 --delete --id id_to_delete --zone zone

Other:
  --server http://your-server
EOT
}
